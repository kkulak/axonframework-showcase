import com.bmuschko.gradle.docker.tasks.image.DockerTagImage
import com.bmuschko.gradle.docker.tasks.image.Dockerfile
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerPushImage

import java.time.LocalDateTime
import java.time.format.DateTimeFormatter


apply plugin: 'spring-boot'
apply plugin: 'com.bmuschko.docker-java-application'
mainClassName = 'knbit.memberquestions.bc.Application'

buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
        maven { url "http://repo.spring.io/milestone" }
        maven { url 'https://oss.sonatype.org/content/repositories/releases/' }
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.2.2.RELEASE")
        classpath 'com.bmuschko:gradle-docker-plugin:2.4'

    }
}

dependencies {
    // knbit-events-bc
    compile project(':events-bc-core')

    // spring
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-mail:1.2.3.RELEASE'

    compile 'commons-validator:commons-validator:1.4.0'

    // swagger
    compile "org.ajar:swagger-spring-mvc-ui:0.4"
    compile 'com.mangofactory:swagger-springmvc:1.0.2'
    runtime 'org.apache.tomcat.embed:tomcat-embed-jasper'
}

docker {
    registryCredentials {
        username = dockerRegistryProperties.username
        password = dockerRegistryProperties.password
        email = dockerRegistryProperties.email
    }
}
task createDockerfile(type: Dockerfile) {
    dependsOn build
    destFile = project.file('build/Dockerfile')
    from 'java:8'
    volume '/tmp'
    addFile 'libs/member-questions-bc-*.jar', 'member-questions-bc.jar'
    runCommand 'bash -c "touch /member-questions-bc.jar"'
    entryPoint 'java', '-Xmx512m', '-jar', '/member-questions-bc.jar'
}

task buildImage(type: DockerBuildImage) {
    dependsOn createDockerfile
    inputDir = createDockerfile.destFile.parentFile
    tag = 'knbitevents/member-questions-bc:latest'
}

task tagImage(type: DockerTagImage) {
    dependsOn buildImage
    repository = 'knbitevents/member-questions-bc'
    imageId = 'knbitevents/member-questions-bc'
    tag = version + '-' + LocalDateTime.now().format(DateTimeFormatter.ofPattern("yyyy-MM-dd_hh.mm.ss"))
}

task pushImage(type: DockerPushImage) {
    dependsOn tagImage
    imageName = 'knbitevents/member-questions-bc'
}

springBoot {
    requiresUnpack = ['org.ajar:swagger-spring-mvc-ui']
}

bootRun {
    systemProperties = System.properties
}